Dataset ticket_23584 {
  label: 'ticket_23584'
  description: '''
In the dashboard, which includes a Category filter, only display categories that have had non-zero sales in the last 2 years,
so users aren't forced to scroll through outdated or irrelevant categories.

"Unused categories" = categories with 0 total sales amount over the past 2 years
=> 1. Filtering logic: total sales amount over the past 2 years
=> 2. Roll it up to the Dim Category's grain
=> 3. Then apply a case when to take the categories' name, Active Category
'''
  data_source_name: 'demodb'

  models: [
    chinh_dim_date,
    chinh_dim_category,
    chinh_dim_product,
    chinh_fact_sales,
  ]

  relationships: [
    relationship(chinh_fact_sales.product_id > chinh_dim_product.product_id, true),
    relationship(chinh_fact_sales.date_id > chinh_dim_date.date_id, true),
    relationship(chinh_dim_product.category_id > chinh_dim_category.category_id, true),
  ]

  metric total_amount {
    label: 'Total Amount'
    type: 'number'
    definition: @aql
      coalesce(sum(chinh_fact_sales.sales_amount), 0)
    ;;
  }

  dimension amount_per_product_ordered_within_2_years { // calculated field
    model: chinh_dim_category
    label: 'Amount per Products (ordered within 2 years)'
    type: 'number'
    definition: @aql
      dimensionalize(
        total_amount
          | where(chinh_fact_sales.created_at > @(last 2 years)),
        chinh_dim_category.category_id
      )
    ;;
  }

  dimension active_category {
    model: chinh_dim_category
    label: 'Active Category'
    type: 'text'
    definition: @aql
      case(
        when: chinh_dim_category.amount_per_product_ordered_within_2_years > 0,
        then: chinh_dim_category.category, // category name

        else: null
      )
    ;;
  }
}
