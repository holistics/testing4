Metric total_users {
  label: 'Total Users'
  type: 'number'
  description: "Total number of users"
  definition: @aql count(ecommerce_users.id) ;;
}
Metric total_orders {
  label: "Total Orders"
  type: "number"
  hidden: false
  description: "Total number of orders placed across the platform, regardless of status."
  definition: @aql count(ecommerce_orders.id);;
}

Metric gmv {
  label: "GMV (Gross Merchandise Value)"
  type: "number"
  hidden: false
  description: "GMV - Gross Merchandise Value: Total value of all orders before discount."
  definition: @aql ecommerce_order_items | sum(ecommerce_order_items.quantity * ecommerce_products.price);;
  format: "[\$\$]#,###0"
}

Metric total_discount {
  label: "Total Discount"
  type: "number"
  hidden: false
  description: "Total Discount: Total discount value of all orders."
  definition: @aql gmv - revenue;;
  format: "[\$\$]#,###0"
}

Metric nmv {
  label: "NMV (Net Merchandise Value)"
  type: "number"
  hidden: false
  description: "* Fulfilled order value after discount. Excludes cancelled/refunded orders. <br>* Represents the total paid value of fulfilled orders, excluding cancelled and refunded orders, and adjusted for any discounts applied. <br>* This reflects what customers actually paid for successfully completed transactions, and serves as the revenue base before applying commission."
  definition: @aql (ecommerce_order_items | sum(ecommerce_order_items.quantity * ecommerce_products.price * (1 - ecommerce_orders.discount))) |  where(ecommerce_orders.status not in ['cancelled', 'refunded']);;
  format: "[\$\$]#,###0"
}

Metric revenue {
  label: "Total Revenue"
  type: "number"
  hidden: false
  description: "Represents the total revenue earned by the platform based on fulfilled order value (NMV), multiplied by a commission rate. <br> As of May 2025, this demo assumes a 50% commission margin. Formula: `revenue = NMV × revenue_commission`"
  definition: @aql nmv * revenue_commission;;
  format: "[\$\$]#,###0"
}

Metric aov {
  label: "AOV (Average Order Value)"
  type: "number"
  description: "AOV - Average Order Value: Average value per order (GMV / number of orders)."
  hidden: false
  definition: @aql gmv / total_orders;;
  format: "[\$\$]#,###0"
}
Metric total_orders_across_all {
  label: "Total Orders Across All"
  type: "number"
  hidden: false
  description: "Total number of orders across all selected dimensions. Used as the denominator for ratio Metrics like percent_of_total."
  definition: @aql total_orders | of_all(ecommerce_orders);;
}
Metric percent_of_total {
  label: "Percent Of Total Orders"
  type: "number"
  hidden: false
  description: "Percentage of orders contributed by the current slice of data compared to the total orders across all segments."
  definition: @aql total_orders * 1.0 / total_orders_across_all;;
  format: "#,###0.00%"
}
Metric total_delivered_orders {
  label: "Total Delivered Orders"
  type: "number"
  hidden: false
  description: "Count of all orders that were successfully delivered."
  definition: @aql total_orders | where(ecommerce_orders.status is 'delivered');;
}
Metric total_refunded_orders {
  label: "Total Refunded Orders"
  type: "number"
  hidden: false
  description: "Count of all orders that were refunded after fulfillment."
  definition: @aql total_orders | where(ecommerce_orders.status is 'refunded');;
}
Metric total_cancelled_orders {
  label: "Total Cancelled Orders"
  type: "number"
  hidden: false
  description: "Count of orders that were cancelled before fulfillment."
  definition: @aql total_orders | where(ecommerce_orders.status is 'cancelled');;
}

Metric cancelled_order_ratio {
  label: "Cancelled Order Ratio"
  type: "number"
  hidden: false
  description: "Percentage of orders that were cancelled - Total number of cancelled orders/Total numbers of orders"
  definition: @aql total_cancelled_orders / total_orders;;
  format: "#,###0.00%"
}

Metric cancelled_value {
  label: "Cancelled Value"
  type: "number"
  hidden: false
  description: "Total Value of Cancelled Orders"
  definition: @aql gmv | where(ecommerce_orders.status is 'cancelled');;
  format: "[\$\$]#,###0"
}

Metric cancelled_value_ratio {
  label: "Cancelled Value Ratio"
  type: "number"
  hidden: false
  description: "Percentage of order values that were cancelled - Total value of cancelled orders/Total revenue"
  definition: @aql cancelled_value / gmv;;
  format: "#,###0.00%"
}

Metric delivered_value {
  label: "Delivered Value"
  type: "number"
  hidden: false
  description: "Total Value of Delivered Orders"
  definition: @aql gmv | where(ecommerce_orders.status is 'delivered');;
  format: "[\$\$]#,###0"
}

Metric refunded_value {
  label: "Refunded Value"
  type: "number"
  hidden: false
  description: "Total Value of Refunded Orders"
  definition: @aql gmv | where(ecommerce_orders.status is 'refunded');;
  format: "[\$\$]#,###0"
}
Metric retention {
  label: 'Cohort Retention'
  type: 'number'
  hidden: false
  description: "Cohort retention rate calculated as the percentage of users in a cohort who placed an order in a given month. *Formula*: current period total_users / cohort\'s total_users."
  definition: @aql (total_users * 1.0) / (total_users | of_all(ecommerce_orders.month_number)) ;;
  format: "#,###0.00%"
}
Metric total_buyers {
  label: "Total Buyers"
  type: "number"
  hidden: false
  description: "Total number of unique users who made at least one purchase."
  definition: @aql count(ecommerce_users.id) | where(ecommerce_users.is_buyer is true);;
}
Metric total_repeated_buyers {
  label: "Total Repeated Buyers"
  type: "number"
  hidden: false
  description: "Total number of users who made more than one purchase."
  definition: @aql count(ecommerce_users.id) | where(ecommerce_users.is_repeated_buyer is true);;
}
Metric running_total_orders {
  label: "Running Total Orders"
  type: "number"
  hidden: false
  description: "Cumulative total of orders over time, aggregated by year to show growth trends."
  definition: @aql running_total(total_orders, ecommerce_orders.created_at | year());;
}

Metric dynamic_metric {
  label: 'Dynamic Metric (no format)'
  type: 'number'
  description: 'This dynamic Metric doesnt take the format into considering, theyre all in their raw form'
  definition: @aql
    case(
      
      when: 'total users' in param_model_demo.Metric_selections
      , then: ecommerce_users.total_users

      , when: 'total orders' in param_model_demo.Metric_selections
      , then: total_orders

      , when: 'platform revenue' in param_model_demo.Metric_selections
      , then: revenue

      , when: 'GMV' in param_model_demo.Metric_selections
      , then: gmv

      , when: 'NMV' in param_model_demo.Metric_selections
      , then: nmv

      , when: 'AOV' in param_model_demo.Metric_selections
      , then: aov

    )
    ;;
}

Metric dynamic_metric_currency {
  label: 'Dynamic Metric (Currency)'
  type: 'number'
  description: 'Only contains list of Metrics with currency (Revenue, GMV, NMV, AOV)'
  definition: @aql
    case(
      when: 'platform revenue' in param_model_demo.Metric_selections_currency
      , then: revenue

      , when: 'GMV' in param_model_demo.Metric_selections_currency
      , then: gmv

      , when: 'NMV' in param_model_demo.Metric_selections_currency
      , then: nmv

      , when: 'AOV' in param_model_demo.Metric_selections_currency
      , then: aov

    )
    ;;
  format: "[\$\$]#,###0"
}

Metric revenue_commission {
  label: "Revenue Commission Rate (Demo from May 2025)"
  type: "number"
  hidden: false
  description: "Commission margin (%) our platform earns on NMV transactions. Set to 50% as of May 2025 for demonstration purposes. Used in revenue calculation: revenue = NMV × commission_rate."
  definition: @aql 0.5;;
  format: "#,###%"
}

PartialDataset volumn_metric {
  metric total_orders: total_orders
  metric total_delivered_orders: total_delivered_orders
  metric total_cancelled_orders: total_cancelled_orders
  metric total_refunded_orders: total_refunded_orders
  metric running_total_orders: running_total_orders 
  metric total_orders_across_all: total_orders_across_all
}

PartialDataset users_metric {
  metric total_users: total_users
  metric total_buyers: total_buyers
  metric total_repeated_buyers: total_repeated_buyers
  metric retention: retention  
}

PartialDataset revenue_metrics {
  metric gmv: gmv
  metric nmv: nmv
  metric revenue: revenue
  metric aov: aov
  metric total_discount: total_discount
  metric revenue_commission: revenue_commission
}

PartialDataset order_value_metric {
  metric delivered_value: delivered_value
  metric cancelled_value: cancelled_value 
  metric refunded_value: refunded_value 
}