Dataset demo_ecommerce_version_base {
  label: '[Demo] Ecommerce (Base)'
  description: 'New dataset base'
  data_source_name: 'demodb'
  models: [
    ecommerce_products,
    ecommerce_merchants,
    ecommerce_countries,
    ecommerce_orders,
    ecommerce_users,
    ecommerce_order_items,
    ecommerce_cities,
    map_categories,
    param_model_demo,
    dim_dates  
  ]
  relationships: [
    relationship(ecommerce_order_items.order_id > ecommerce_orders.id, true),
    relationship(ecommerce_orders.user_id > ecommerce_users.id, true),
    relationship(ecommerce_users.city_id > ecommerce_cities.id, true),
    relationship(ecommerce_cities.country_code > ecommerce_countries.code, true),
    relationship(ecommerce_order_items.product_id > ecommerce_products.id, true),
    // relationship(ecommerce_products.id - ecommerce_product_images.product_id, true),
    relationship(ecommerce_products.merchant_id > ecommerce_merchants.id, true),
    relationship(ecommerce_products_map_categories, true),
    relationship(ecommerce_orders_dim_dates, true),
    relationship(ecommerce_users.sign_up_date > dim_dates.date_key, false)
    ,
    relationship(ecommerce_merchants.city_id > ecommerce_cities.id, false)
    ]

  permission vendor_access {
    field: ref('ecommerce_merchants', 'id')
    operator: 'matches_user_attribute'
    value: 'vendor_id'
  }

  permission country_access {
    field: ref('ecommerce_countries', 'code')
    operator: 'matches_user_attribute'
    value: 'country'
  }

  permission city_access {
    field: ref('ecommerce_cities', 'name')
    operator: 'matches_user_attribute'
    value: 'city'
  }

  dimension cohort_month {
    model: ecommerce_users
    label: "Cohort Month"
    type: 'date'
    definition: @aql min(ecommerce_orders.created_at | month()) | dimensionalize(ecommerce_users.id);;
  }

  dimension month_number {
    model: ecommerce_orders
    label: 'Month Number'
    type: 'number'
    definition: @aql date_diff('month', ecommerce_users.cohort_month, (ecommerce_orders.created_at | month())) ;;
  }

  dimension cohort_size {
    model: ecommerce_orders
    label: 'Cohort Size'
    type: 'number'
    definition: @aql count_distinct(ecommerce_orders.user_id) | exact_grains(ecommerce_users.cohort_month) ;;
  }

  dimension total_orders_all_time {
    model: ecommerce_users
    label: 'Total Orders (Users)'
    type: 'number'
    definition: @aql total_orders | dimensionalize(ecommerce_users.id);;
  }

  dimension is_buyer {
    model: ecommerce_users
    label: 'Is Buyer'
    type: 'truefalse'
    definition: @aql ecommerce_users.total_orders_all_time > 0;;
  }

  dimension is_repeated_buyer {
    model: ecommerce_users
    label: 'Is Repeated Buyer'
    type: 'truefalse'
    definition: @aql ecommerce_users.total_orders_all_time > 1;;
  }

  dimension product_performance_analysis {
    model: ecommerce_products
    label: 'Product Performance Analysis'
    type: 'text'
    definition: @aql concat(
      'https://claude.ai/new?q=write+an_analysis+for+',
      ecommerce_products.name,
      '.+The+GMV+for+this+product+is+',
      cast(gmv, 'text'),
      '+and+the+AOV+for+this+product+is+',
      cast(aov, 'text'),
      )
     ;;
  }

  dimension product_image {
    model: ecommerce_product_images
    label: 'Product Image'
    type: 'text'
    definition: @aql concat('<img src="', ecommerce_product_images.url, '" style="text-align:center;height:50px;object-fit:cover" />') ;;
  }

  dimension breakdown_dim {
    label: 'Dynamic Breakdown Dimension'
    description: 'The dimension will be automatically switched among Country, Age Group, Order Status. By default, it will be Country'
    type: 'text'
    model: ecommerce_orders
    definition: @aql case(
        when: 'Country Names' in param_model_demo.dim_selections
        , then: ecommerce_countries.name

        , when: 'Age Demographic' in param_model_demo.dim_selections
        , then: ecommerce_users.age_group

        , when: 'Order Status' in param_model_demo.dim_selections
        , then: ecommerce_orders.status

        , else: ecommerce_countries.name
      ) ;;
  }

  owner: 'vincent@holistics.io'
}